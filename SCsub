#!/usr/bin/env python

import os
from core import components

Import("env")
env_goost = env.Clone()

# Thirdparty sources
thirdparty_dirs = [
    "thirdparty/clipper/",
    "thirdparty/exoquant/",
    "thirdparty/hqx/",
]

env_thirdparty = env_goost.Clone()
for d in thirdparty_dirs:
    env_thirdparty.add_source_files(env.modules_sources, d + "*.cpp")
    env_thirdparty.add_source_files(env.modules_sources, d + "*.cc")
    env_thirdparty.add_source_files(env.modules_sources, d + "*.c")
env_thirdparty.disable_warnings()
thirdparty_path = os.path.realpath("thirdparty")
env_thirdparty.Prepend(CPPPATH=[thirdparty_path])

env_goost.Prepend(CPPPATH=[thirdparty_path])

goost_path = os.path.dirname(os.path.realpath(os.curdir))
env_goost.Prepend(CPPPATH=[goost_path])

for name in components:
    opt = "goost_%s_enabled" % (name)
    if env_goost[opt]:
        env_goost.Prepend(CPPDEFINES=[opt.upper()])

env_goost.Prepend(CPPDEFINES={"SCALE_FACTOR" : env_goost["goost_scale_factor"]})

# Build subdirs, the build order is dependent on the link order.
subdirs = [
    "core",
    "scene",
    "editor",
    # "modules", # Not build directly, use `custom_modules` option explicitly.
]
SConscript(dirs=subdirs, name="SCsub", exports="env_goost")

env_goost.add_source_files(env.modules_sources, "*.cpp")
